package com.gsmart.ui.components

import javafx.scene.layout.VBox
import javafx.scene.layout.HBox
import javafx.scene.control.Label
import javafx.scene.control.TitledPane
import javafx.scene.control.TextField
import javafx.scene.control.DatePicker
import javafx.scene.control.TextArea
import javafx.scene.control.Button
import javafx.geometry.Insets
import javafx.scene.text.Text
import com.gsmart.ui.components.OrderTablePane
import javafx.scene.control.TableView
import com.gsmart.ui.components.RoomOrderTable
import com.gsmart.ui.controller.OrderRoomController
import javafx.scene.image.ImageView
import javafx.scene.image.Image
import javafx.scene.text.Font
import com.gsmart.ui.controls.FXTextField
import com.gsmart.ui.controls.FXDateTimePicker
import de.jensd.fx.glyphs.materialdesignicons.MaterialDesignIconView
import javafx.scene.paint.Color

component OrderRoomStage controlledby OrderRoomController styledwith [ "css/application.css",
	"css/order-room-stage.css", "css/material-fx-v0_3.css" ]  resourcefile "locale/messages_vi_VN.properties"{
	VBox {
		styleClass : [
			"order-room-stage"
		],
		HBox {
			styleClass : [
				"top-bar"
			],
			Label id titleStage {
				styleClass : [
					"header-label"
				],
				graphic : ImageView {
					image : Image {
						url : location "images/order-room.png"
					},
					fitHeight : 35,
					fitWidth : 35
				},
				text : rstring "OrderRoomStage.Label.CreateNewOrder"
			}
		},
		HBox {
			spacing : 10,
			style : "-fx-padding : 5px;",
			TitledPane {
				styleClass : [
					"titled-pane"
				],
				collapsible : false,
				text : rstring "OrderRoomStage.Label.RoomInfoHeader",
				VBox {
					styleClass : [
						"card"
					],
					spacing : -10,
					FXDateTimePicker id dateTimePicker {
						isInlineStyle : false,
						firstDateLabel : "Date in",
						firstTimeLabel : "Time in",
						secondDateLabel : "Date out",
						secondTimeLabel : "Time out"
					},
					FXTextField id prepayTxt {
						isInlineStyle : true,
						label : rstring "OrderRoomStage.Prepay",
						unit : " VND",
						fieldName : "prepay"
					},
					FXTextField id promotionTxt {
						isInlineStyle : true,
						label : rstring "OrderRoomStage.Promotion",
						unit : " %",
						fieldName : "promotion"
					},
					HBox {
						Label {
							styleClass : [
								"control-label"
							],
							text : rstring "OrderRoomStage.Notice"
						},
						TextArea id registrationNotice {
							prefWidth : 350,
							prefHeight : 30
						}
					},
					HBox {
						style : "-fx-padding : 20px 0px 0px 0px;",
						FXTextField id roomTxt {
							label : rstring "OrderRoomStage.Room",
							fieldName : "room",
							editAble : false
						},
						Button id quickSearchBtn{
							onAction : controllermethod openQuickSeachRoomStage,
							styleClass : [
								"quick-search-button"
							],
							text : rstring "OrderRoomStage.Button.QuickSearch",
							graphic : MaterialDesignIconView {
								glyphName : "MAGNIFY_PLUS",
								size : "2em",
								fill : "rgb(255,255,255)"
							}
						}
					}
				}
			},
			TitledPane {
				prefHeight : 390,
				styleClass : [
					"titled-pane"
				],
				collapsible : false,
				text : rstring "OrderRoomStage.Label.CustomerInfoHeader",
				VBox {
					styleClass : [
						"card"
					],
					FXTextField id customerNameTxt {
						label : rstring "OrderRoomStage.Name",
						fieldName : "customerName"
					},
					FXTextField id customerIDTxt {
						label : rstring "OrderRoomStage.ID",
						fieldName : "customerId"
					},
					FXTextField id customerAddress {
						label : rstring "OrderRoomStage.Address",
						fieldName : "customerAddress"
					},
					FXTextField id customerTelephone {
						label : rstring "OrderRoomStage.Telephone",
						fieldName : "customerTelephone"
					},
					HBox {
						Label {
							styleClass : [
								"control-label"
							],
							text : rstring "OrderRoomStage.Notice"
						},
						TextArea id customerNotice {
							prefWidth : 100,
							prefHeight : 50
						}
					}
				}
			}
		},
		HBox {
			spacing : 10,
			style : "-fx-padding : 10px;",
			Button id saveBtn {
				styleClass : [
					"add-button"
				],
				text : rstring "OrderRoomStage.Button.Save",
				graphic : MaterialDesignIconView {
					glyphName : "CONTENT_SAVE",
					size : "2em",
					fill : "rgb(255,255,255)"
				},
				onAction : controllermethod saveOrder
			},
			Button id resetBtn {
				styleClass : [
					"reset-button"
				],
				text : rstring "OrderRoomStage.Button.Reset",
				graphic : MaterialDesignIconView {
					glyphName : "REFRESH",
					size : "2em",
					fill : "rgb(255,255,255)"
				},
				onAction : controllermethod resetOrderForm
			}
		},
		HBox {
			spacing : 10,
			style : "-fx-padding : 10px;",
			Text {
				styleClass : [
					"order-table-label"
				],
				text : rstring "UIControls.OrderTablePane.ListCurrentOrderInRoomLabel"
			}
		},
		RoomOrderTable id roomOrderTable {
			style : "-fx-padding : 10px;",
			maxHeight : 200
		}
	}
}
